head	1.5;
access;
symbols
	MLWorks_21c0_1999_03_25:1.5
	MLWorks_20c1_1998_08_20:1.5
	MLWorks_20c0_1998_08_04:1.5
	MLWorks_20b2c2_1998_06_19:1.5
	MLWorks_20b2_Windows_1998_06_12:1.5
	MLWorks_20b1c1_1998_05_07:1.5
	MLWorks_20b0_1998_04_07:1.5
	MLWorks_20b0_1998_03_20:1.5
	MLWorks_20m2_1998_02_16:1.5
	MLWorks_workspace_97:1.5.2
	MLWorks_20m1_1997_10_23:1.5
	MLWorks_11r1:1.5.1.1.1.1.1
	MLWorks_11c0_1997_09_09:1.5.1.1.1.1
	MLWorks_10r3:1.5.1.1.3
	MLWorks_10r2_551:1.5.1.1.2
	MLWorks_11:1.5.1.1.1
	MLWorks_1_0_r2c2_1997_07_28:1.5.1.1
	MLWorks_20m0_1997_06_20:1.5
	MLWorks_1_0_r2c2_1997_06_14:1.5.1.1
	MLWorks_1_0_r2c1_released_1997_05_23:1.5.1.1
	MLWorks_1_0_r2c1_1997_05_12:1.5.1
	MLWorks_BugFix_1997_04_24:1.5
	MLWorks_1_0_r2_Win32_1997_04_11:1.5
	MLWorks_1_0_r2_Unix_1997_04_04:1.5
	MLWorks_1_0_1_ULTRASPARC_1997_02_24:1.4.6.1.1
	MLWorks_1_0_Win32_1996_12_17:1.4.6
	MLWorks_1_0_Irix_1996_11_28_released_1996_12_17:1.4.4.1.1.1
	MLWorks_1_0_Unix_1996_11_14_released_1996_12_17:1.4.4.1
	MLWorks_1_0_Irix_1996_11_28:1.4.4.1.1
	MLWorks_1_0_Win32_1996_11_22:1.4.5
	MLWorks_1_0_Unix_1996_11_14:1.4.4
	MLWorks_Open_Beta2_1996_10_11:1.4.3
	MLWorks_License_dev:1.4.2
	MLWorks_1_open_beta_1996_09_13:1.4.1
	MLWorks_Open_Beta_1996_08_22:1.3;
locks; strict;
comment	@ *  @;


1.5
date	96.11.16.11.30.55;	author io;	state Exp;
branches
	1.5.1.1
	1.5.2.1;
next	1.4;

1.4
date	96.09.04.11.54.50;	author jont;	state Exp;
branches
	1.4.1.1
	1.4.2.1
	1.4.3.1
	1.4.4.1
	1.4.5.1
	1.4.6.1;
next	1.3;

1.3
date	96.08.13.09.48.23;	author davids;	state Exp;
branches;
next	1.2;

1.2
date	96.08.12.14.02.38;	author davids;	state Exp;
branches;
next	1.1;

1.1
date	96.08.09.15.52.59;	author davids;	state Exp;
branches;
next	;

1.4.1.1
date	96.09.13.11.07.44;	author hope;	state Exp;
branches;
next	;

1.4.2.1
date	96.10.07.15.58.55;	author hope;	state Exp;
branches;
next	;

1.4.3.1
date	96.10.17.11.17.15;	author hope;	state Exp;
branches;
next	;

1.4.4.1
date	96.11.14.12.40.44;	author hope;	state Exp;
branches
	1.4.4.1.1.1;
next	;

1.4.4.1.1.1
date	96.11.28.14.52.35;	author hope;	state Exp;
branches;
next	;

1.4.5.1
date	96.11.22.18.01.12;	author hope;	state Exp;
branches;
next	;

1.4.6.1
date	96.12.17.17.40.05;	author hope;	state Exp;
branches
	1.4.6.1.1.1;
next	;

1.4.6.1.1.1
date	97.02.24.11.27.18;	author hope;	state Exp;
branches;
next	;

1.5.1.1
date	97.05.12.10.25.06;	author hope;	state Exp;
branches
	1.5.1.1.1.1
	1.5.1.1.2.1
	1.5.1.1.3.1;
next	;

1.5.1.1.1.1
date	97.07.28.18.11.01;	author daveb;	state Exp;
branches
	1.5.1.1.1.1.1.1;
next	;

1.5.1.1.1.1.1.1
date	97.10.07.11.35.34;	author jkbrook;	state Exp;
branches;
next	;

1.5.1.1.2.1
date	97.09.08.17.04.21;	author daveb;	state Exp;
branches;
next	;

1.5.1.1.3.1
date	97.09.09.13.59.23;	author daveb;	state Exp;
branches;
next	;

1.5.2.1
date	97.11.30.16.49.23;	author daveb;	state Exp;
branches;
next	;


desc
@new unit
@


1.5
log
@[Bug #1757]
renamed __char{array,vector}
@
text
@(*  ==== BASIS EXAMPLES : Puzzle structure ====
 *
 *  Copyright (C) 1996 Harlequin Ltd.
 *
 *  Description
 *  -----------
 *  This module solves "criss-cross" puzzles - puzzles in which a list of
 *  words must be fitted into a crossword grid.  It demonstrates the use of
 *  the CharVector and CharArray structures in the basis library.
 *
 *  Revision Log
 *  ------------
 *  $Log: basis:__puzzle.sml,v $
 *  Revision 1.4  1996/09/04  11:54:50  jont
 *  Make require statements absolute
 *
 *  Revision 1.3  1996/08/13  09:48:23  davids
 *  ** No reason given. **
 *
 *  Revision 1.2  1996/08/12  14:02:38  davids
 *  Added heuristic to sort words into an order that will be faster.
 *
 *  Revision 1.1  1996/08/09  15:52:59  davids
 *  new unit
 *
 *
 *)


(* NB. The use of 'word' in this module corresponds to the usual English
 meaning of "word" and not to the SML word type. *)

require "puzzle";
require "$.basis.__char_vector";
require "$.basis.__char_array";

structure Puzzle : PUZZLE =
  struct

    (* This type represents the direction in which a word reads in the grid. *)

    datatype direction = HORIZ | VERT


    (* This type represents a gap in the grid which a word may fit into. *)

    datatype gap = GAP of {dir: direction,
			   x: int,
			   y: int,
			   length: int}


    (* This type represents a given criss-cross puzzle.  The grid is a
     character array holding the current state of the puzzle, where spaces
     are represented as #"#" and the puzzle walls as #" ". *)

    datatype puzzle = PUZZLE of {grid : CharArray.array,
				 width : int,
				 height : int,
				 words : string list,
				 gaps : gap list}


    (* This type holds the list of the minimum characters that would need to
     be updated should a given word be placed onto the grid.  It also
     specifies which gap in the gap list would be used. *)  

    datatype placement = PLACE of {updates : (char * int) list,
				   gap : int}

 
    (* Remove the 'n'th element of a list. *)

    fun removeNth ([], _) = raise Empty
      | removeNth (h::t, 1) = t
      | removeNth (h::t, n) = h :: removeNth (t, n - 1)
   

    (* Find the array index to which the co-ordinates (x, y) refer. *)

    fun getPos (PUZZLE {width = width, ...}, x, y) = y * width + x


    (* Find a cell in the puzzle grid with co-ordinates (x, y) *)

    fun getCell (PUZZLE {grid = grid, width = width, ...}, x, y) =
      CharArray.sub (grid, y * width + x)

    
    (* Update the puzzle grid with the characters specified. *)

    fun place (PUZZLE {grid = grid, ...},
	       PLACE {updates = updates, ...}) =
      let
	fun placeChars [] = ()
	  | placeChars ((chr, pos)::t) =
	    (CharArray.update (grid, pos, chr);
	     placeChars t)
      in
	placeChars updates
      end


    (* Remove all characters specified from the puzzle grid. *)

    fun unplace (PUZZLE {grid = grid, ...},
		 PLACE {updates = updates, ...}) =
      let
	fun unplaceChars [] = ()
	  | unplaceChars ((chr, pos)::t) =
	    (CharArray.update (grid, pos, #"#");
	     unplaceChars t)
      in
	unplaceChars updates
      end


    (* Determine whether 'word' will fit correctly into the given gap in
     'puzzle'.  Return the PLACE values in this case, ie. the list of 
     positions that would be written in with characters if this word
     was placed in the grid. *)

    fun matchGap' (puzzle as PUZZLE {grid = grid, ...},
		   GAP {dir = dir, x = xStart, y = yStart, length = length},
		   word) =
      let
	(* Check if the 'i'th character of 'word' matches the character in the
	 grid.  If the character is #"#" then it matches automatically. *)
	fun matchChar (i, places) =
	  if i >= length then
	    SOME places
	  else
	    let
	      (* Find the index into grid for the current character. *)
	      val gridPos = case dir of
		HORIZ => getPos (puzzle, xStart + i, yStart)
	      | VERT => getPos (puzzle, xStart, yStart + i)

	      (* Find the character held in this position in the grid. *)
	      val gridChar = case dir of 
		HORIZ => getCell (puzzle, xStart + i, yStart)
	      | VERT => getCell (puzzle, xStart, yStart + i)

	      val wordChar = CharVector.sub (word, i)
	    in
	      case gridChar of
		#" " => NONE
	      | #"#" => matchChar (i + 1, (wordChar, gridPos) :: places)
	      | c => 
		  if wordChar = c then 
		    matchChar (i + 1, places)
		  else
		    NONE
	    end
      in
	matchChar (0, [])
      end
	
    
    (* If 'word' is a different length to the gap, then return NONE
     immediately.  Otherwise check the individual characters. *)

    fun matchGap (puzzle, gap as GAP {length = length, ...}, word) =
      if length <> (CharVector.length word) then
	NONE
      else
	matchGap' (puzzle, gap, word)


    (* Find all gaps in 'puzzle' that 'word' will fit in correctly, and return
     the PLACE values for each of these. *)

    fun fitWord (puzzle as PUZZLE {gaps = gaps, ...}, word) =
      let
	fun fitGap ([], _) = []
	  | fitGap (h::t, gapNum) =
	      case matchGap (puzzle, h, word) of
		NONE => fitGap (t, gapNum + 1)
	      | SOME places => PLACE {updates = places,
				      gap = gapNum} :: fitGap (t, gapNum + 1)
      in
	fitGap (gaps, 1)
      end
    

    (* Print out the grid for the given puzzle. *)

    fun printGrid (PUZZLE {grid = grid,
			   width = width,
			   height = height,
			   ...}) = 
      let
	fun printLine h =
	  if h >= height then
	    ()
	  else
	    (print (CharArray.extract (grid, h * width, SOME width) ^ "\n");
	     printLine (h + 1))
      in
	(print "\nSolution:\n\n";
	 printLine 0)
      end


    (* Solve 'puzzle'.  This is done by a purely brute force technique of
     trying to fit each word into every possible gap.  If it fits, then
     the grid is updated and 'solve' is called recursively.  The updates on
     'grid' are then undone, so that backtracking can occur.  Once all the
     words have been placed the puzzle is solved, so print out the grid. *)

    fun solvePuzzle (puzzle as PUZZLE {words = words, ...}) =
      case words of
	[] => printGrid puzzle
      | (word :: rest) => placeWord (puzzle, fitWord (puzzle, word), rest)

    (* For each possible placing of the word, update the grid, call 'solve'
     recursively, and then undo the updates. *)

    and placeWord (puzzle as PUZZLE {grid = grid,
				     width = width,
				     height = height,
				     words = words,
				     gaps = gaps},
		   places, 
		   otherWords) =
      case places of
	[] => ()
      | ((headPlace as PLACE {gap = gap, ...}) :: tailPlace) => 
	  (place (puzzle, headPlace);
	   solvePuzzle (PUZZLE {grid = grid,
			  width = width,
			  height = height,
			  words = otherWords,
			  gaps = removeNth (gaps, gap)});
	   unplace (puzzle, headPlace);
	   placeWord (puzzle, tailPlace, otherWords))


    (* Find all the gaps in 'puzzle', returning a new puzzle with this field
     filled in appropriately. *)

    fun findGaps (puzzle as PUZZLE {grid = grid,
				    width = width,
				    height = height,
				    words = words,
				    gaps = gaps}) =
      let
	(* Only cons a value onto the list 'l' if it is SOME v. *)

	fun consOption (NONE, l) = l
	  | consOption (SOME v, l) = v :: l

	(* Work through the grid left to right, top to bottom, finding the
	 list of all horizontal sequences of spaces that have a length > 1. *)

	fun findHorizGaps (x, y, blanks) =
	  let
	    fun horizGap () =
	      if blanks > 1 then
		SOME (GAP {dir = HORIZ,
			   x = x - blanks,
			   y = y,
			   length = blanks}) 
	      else
		NONE
	  in
	    if y >= height then
	      []
	    else
	      if x >= width then
		consOption (horizGap (), findHorizGaps (0, y + 1, 0))
	      else
		if getCell (puzzle, x, y) <> #" " then
		  findHorizGaps (x + 1, y, blanks + 1)
		else
		  consOption (horizGap (), findHorizGaps (x + 1, y, 0))
	  end

	(* Work through the grid top to bottom, left to right, finding the
	 list of all vertical sequences of spaces that have length > 1. *)

	fun findVertGaps (x, y, blanks) =
	  let
	    fun vertGap () =
	      if blanks > 1 then
		SOME (GAP {dir = VERT,
			   x = x,
			   y = y - blanks,
			   length = blanks})
	      else
		NONE
	  in
	    if x >= width then
	      []
	    else
	      if y >= height then
		consOption (vertGap (), findVertGaps (x + 1, 0, 0))
	      else
		if getCell (puzzle, x, y) <> #" " then
		  findVertGaps (x, y + 1, blanks + 1)
		else
		  consOption (vertGap (), findVertGaps (x, y + 1, 0))
	  end
      in
	PUZZLE {grid = grid,
		width = width,
		height = height, 
		words = words,
		gaps = findHorizGaps (0, 0, 0) @@ findVertGaps (0, 0, 0)}
      end
      

    local

      (* Sort a list using insertion sort.  Sort on the function 'compare'. *)

      fun sort (l, compare) =
	let
	  fun ins (x, []) = [x]
	    | ins (x, y::ys) = 
	      if compare (x, y) = GREATER then
		y::ins (x, ys)
	      else
		x::y::ys

	  fun insort [] = []
	    | insort (x::xs) = ins (x, insort xs)
	in
	  insort l
	end

      (* Compare function for comparing lengths of strings. *)

      fun compareStrings (s1, s2) =
	if size s1 > size s2 then GREATER
	  else if size s1 = size s2 then EQUAL
	    else LESS

      (* Compare function for comparing lengths of lists. *)

       fun compareLists (l1, l2) =
	 if length l1 > length l2 then GREATER
	   else if length l1 = length l2 then EQUAL
	     else LESS

      (* Separate out a list into lists for each different length string. *)

      fun separate ([], _, acc) = acc
	| separate (_, _, []) = raise Empty
	| separate (h::t, length, ah::at) =
	  if size h = length then
	    separate (t, length, (h::ah)::at)
	  else
	    separate (t, size h, [h]::ah::at)

      (* Join a list of lists into a single list. *)
      
      fun join [] = []
	| join (h::t) = h @@ join t

    in
      (* Sort the words into an order that allows the puzzle to be solved
       faster - with words of a unique length at the front of the list and
       those with many words of the same length at the back. *)
      
      fun orderWords words = 
	let
	  val sortWords = sort (words, compareStrings)
	  val separateWords = separate (sortWords, ~1, [[]])
	  val orderedWords = sort (separateWords, compareLists)
	in
	  join orderedWords
	end
    end


    (* Find the width of the lines in 'lineList'.  If they are not all the
     same width then raise Size. *)

    fun getWidth lineList =
      let
	val width = CharVector.length (hd lineList)
	fun getWidth' ([], size) = size
	  | getWidth' (h::t, size) =
	    if CharVector.length h = size then
	      getWidth' (t, size)
	    else
	      raise Size
      in
	getWidth' (lineList, width)
      end


    (* Create a puzzle from a list of strings ('lineList') representing the
     original grid, and from a list of strings ('wordList') giving the words
     to be placed in that grid. *)

    fun makePuzzle (lineList, wordList) =
      let
	val width = getWidth lineList
	val height = length lineList
	val words = orderWords wordList
	val gridVec = CharVector.concat lineList
	val grid = CharArray.array (CharVector.length gridVec, #" ")
	val _ = CharArray.copyVec {src = gridVec,
				   si = 0,
				   len = NONE,
				   dst = grid,
				   di = 0}
      in
	findGaps (PUZZLE {grid = grid, 
			  width = width,
			  height = height,
			  words = words,
			  gaps = []})
      end


    (* Solve the given puzzle.  'lineList' represents the original grid, and
     'wordList' represents the words to be placed into the grid. *)

    fun solve (lineList, wordList) =
      solvePuzzle (makePuzzle (lineList, wordList))


    (* A small example puzzle. *)

    val smallPuzzle =
      (["##### #",
	"# # # #",
	"# # ###",
	"##### #",
	"### ###",
	"###   #"],

       ["APPLE", "AWE", "BUBBLE", "EAT", "LABEL", "OWL",
	"PIT", "ROTATE", "TEE", "ZEALOT", "ZEBRA"])


    (* This puzzle is taken from "Kriss Kross" magazine. *)

    val mediumPuzzle =
      (["      #  # #### #  ",
	"      #  #    # # #",
	"#   # #  #    # # #",
	"#   ###############",
	"##### #  # #  # # #",
	"#   #    # #  # # #",
	"    #      #    # #",
	"    #  #######  # #",
	"  #####    #    # #",
	"    #    ###### # #",
	"    #  #   #    # #",
	" ######### #      #",
	"  # #  #  ######   ",
	"  # #    #         ",
	" ########### ####  ",
	"  #      # #  #    ",
	"  #      # ######  ",
	"########   #  #    ",
	"  #        #  #####",
	"########## #       "],

       ["BAR", "BILL", "CHEF", "LIFT", "MAID", "FOYER", "GUARD", "GUEST",
	"HOTEL", "LUNCH", "BARMAN", "DINNER", "PORTER", "STAIRS",
	"TOWELS", "WAITER", "CARPARK", "WAITRESS", "BREAKFAST", "RECEPTION", 
	"SINGLEROOM", "RESTAURANT", "CHAMBERMAID", "SITTINGROOM", 
	"TELEPHONIST", "SERVICECHARGE", "EARLYMORNINGTEA"])

	
    (* This puzzle is taken from "Kriss Kross" magazine. *)

    val largePuzzle =
      (["  #########  ######## #  #",
	"     #    #  #        #  #",
	"   ####   #  #        #  #",
	" #   #    ############## #",
	" #   #    #  # #      #  #",
	"###########  # ###### #  #",
	" #   #    #  # #      #  #",
	" #   #    #  # #   #  ####",
	" # # # ####    ###### #   ",
	" # # #         #   #  # # ",
	" # # # #     ##### #  # # ",
	" # ########    #  ########",
	"   # # #    #  #   #    # ",
	"   #   #    #   ######  # ",
	"   #        #      #    # ",
	"#################  #    # ",
	"   #        #           # ",
	"#########   #     ########",
	"#         # #         # # ",
	"#    #  ######        # # ",
	"#    #    # #         # # ",
	"#    #    # #   ####### # ",
	"#    #      #         # # ",
	"   ##########         # # ",
	"     #         ######## # "],

       ["ANTS", "MICE", "RATS", "RUST", "SCAB", "SLUGS", "APHIDS", "CAPSID",
	"EARWIG", "MILDEW", "SNAILS", "WEEVIL", "TERMITE", "BLACKFLY",
	"CLUBROOT", "CUTWORMS", "GREENFLY", "LEAFSPOT", "WHITEFLY",
	"WIREWORM", "WOODLICE", "APPLESCAB", "BLACKSPOT", "CARROTFLY", 
	"PEAWEEVIL", "TULIPFIRE", "FROGHOPPER", "GREENMOULD", "REDSPIDERS",
	"FLEABEETLES", "CATERPILLARS", "CODLINGMOTHS", "LEATHERJACKET",
	"CABBAGEROOTFLY", "GOOSEBERRYSAWFLY", "RASPBERRYCANESPOT"])

  end
@


1.5.2.1
log
@branched from trunk for label MLWorks_workspace_97
@
text
@a13 4
 *  Revision 1.5  1996/11/16  11:30:55  io
 *  [Bug #1757]
 *  renamed __char{array,vector}
 *
@


1.5.1.1
log
@branched from 1.5
@
text
@a13 4
 *  Revision 1.5  1996/11/16  11:30:55  io
 *  [Bug #1757]
 *  renamed __char{array,vector}
 *
@


1.5.1.1.3.1
log
@branched from MLWorks_1_0_r2c1_1997_05_12 for label MLWorks_10r3
@
text
@a13 3
 *  Revision 1.5.1.1  1997/05/12  10:25:06  hope
 *  branched from 1.5
 *
@


1.5.1.1.2.1
log
@branched from MLWorks_1_0_r2c1_1997_05_12 for label MLWorks_10r2_551
@
text
@a13 3
 *  Revision 1.5.1.1  1997/05/12  10:25:06  hope
 *  branched from 1.5
 *
@


1.5.1.1.1.1
log
@branched from MLWorks_1_0_r2c1_1997_05_12 for label MLWorks_11
@
text
@a13 3
 *  Revision 1.5.1.1  1997/05/12  10:25:06  hope
 *  branched from 1.5
 *
@


1.5.1.1.1.1.1.1
log
@branched from MLWorks_11 for label MLWorks_11r1
@
text
@a13 3
 *  Revision 1.5.1.1.1.1  1997/07/28  18:11:01  daveb
 *  branched from MLWorks_1_0_r2c1_1997_05_12 for label MLWorks_11
 *
@


1.4
log
@Make require statements absolute
@
text
@d14 3
d34 2
a35 2
require "$.basis.__charvector";
require "$.basis.__chararray";
@


1.4.6.1
log
@branched from 1.4
@
text
@a13 3
 *  Revision 1.4  1996/09/04  11:54:50  jont
 *  Make require statements absolute
 *
@


1.4.6.1.1.1
log
@branched from 1.4.6.1
@
text
@a13 3
 *  Revision 1.4.6.1  1996/12/17  17:40:05  hope
 *  branched from 1.4
 *
@


1.4.5.1
log
@branched from 1.4
@
text
@a13 3
 *  Revision 1.4  1996/09/04  11:54:50  jont
 *  Make require statements absolute
 *
@


1.4.4.1
log
@branched from 1.4
@
text
@a13 3
 *  Revision 1.4  1996/09/04  11:54:50  jont
 *  Make require statements absolute
 *
@


1.4.4.1.1.1
log
@branched from 1.4.4.1
@
text
@a13 3
 *  Revision 1.4.4.1  1996/11/14  12:40:44  hope
 *  branched from 1.4
 *
@


1.4.3.1
log
@branched from 1.4
@
text
@a13 3
 *  Revision 1.4  1996/09/04  11:54:50  jont
 *  Make require statements absolute
 *
@


1.4.2.1
log
@branched from 1.4
@
text
@a13 3
 *  Revision 1.4  1996/09/04  11:54:50  jont
 *  Make require statements absolute
 *
@


1.4.1.1
log
@branched from 1.4
@
text
@a13 3
 *  Revision 1.4  1996/09/04  11:54:50  jont
 *  Make require statements absolute
 *
@


1.3
log
@** No reason given. **
@
text
@d14 3
d31 2
a32 2
require "basis.__charvector";
require "basis.__chararray";
@


1.2
log
@Added heuristic to order words according to the frequency of that length.
@
text
@d14 3
d24 3
d31 1
a31 1
structure Puzzle =
d35 1
d40 1
d50 1
d61 1
@


1.1
log
@new unit
@
text
@d13 4
a16 1
 *  $Log$
d25 1
a25 1
structure Puzzle : PUZZLE =
d297 63
d386 1
d398 1
a398 1
			  words = wordList,
d455 1
a455 2
    (* This puzzle is taken from "Kriss Kross" magazine.  It may take a few
     minutes to solve. *)
a492 2


@
